plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
}

group = 'com.internship'
version = '0.0.1-SNAPSHOT'
description = 'HR-Management'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    implementation("me.paulschwarz:spring-dotenv:3.0.0")
    implementation("org.flywaydb:flyway-core:10.10.0")
    implementation("org.flywaydb:flyway-mysql:10.10.0")
    implementation("org.springframework.boot:spring-boot-starter-validation:3.5.6")
    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation("org.dbunit:dbunit:2.7.3")
    testImplementation("com.github.database-rider:rider-spring:1.41.0")
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter(tasks.test)
}

// Checkstyle Configuration
checkstyle {
    toolVersion = "10.12.0"
    configFile = file("config/checkstyle/checkstyle.xml")
    // Build fails on violations
    ignoreFailures = false
}

// Checkstyle for integrationTest source set
tasks.register('checkstyleIntegration', Checkstyle) {
    description = 'Runs Checkstyle on integration tests.'
    group = 'verification'
    source = sourceSets.integrationTest.allJava
    classpath = sourceSets.integrationTest.compileClasspath
    configFile = file("config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    reports {
        xml.required = true
        html.required = true
    }
}

// Ensure check task runs all Checkstyle tasks
tasks.check {
    dependsOn 'checkstyleMain', 'checkstyleTest', 'checkstyleIntegration'
}
